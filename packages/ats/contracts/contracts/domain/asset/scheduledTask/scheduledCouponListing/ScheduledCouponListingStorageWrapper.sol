// SPDX-License-Identifier: Apache-2.0
pragma solidity >=0.8.0 <0.9.0;

import { ScheduledSnapshotsStorageWrapper } from "../scheduledSnapshot/ScheduledSnapshotsStorageWrapper.sol";
import { ScheduledTasksLib } from "../../../../facets/layer_2/scheduledTask/ScheduledTasksLib.sol";
import { _SCHEDULED_COUPON_LISTING_STORAGE_POSITION } from "../../../../constants/storagePositions.sol";
import {
    ScheduledTask,
    ScheduledTasksDataStorage
} from "../../../../facets/layer_2/scheduledTask/scheduledTasksCommon/IScheduledTasksCommon.sol";
import { COUPON_LISTING_RESULT_ID } from "../../../../constants/values.sol";

abstract contract ScheduledCouponListingStorageWrapper is ScheduledSnapshotsStorageWrapper {
    function _addScheduledCouponListing(uint256 _newScheduledTimestamp, bytes32 _actionId) internal override {
        ScheduledTasksLib.addScheduledTask(
            _scheduledCouponListingStorage(),
            _newScheduledTimestamp,
            abi.encode(_actionId)
        );
    }

    function _triggerScheduledCouponListing(uint256 _max) internal override returns (uint256) {
        return
            _triggerScheduledTasks(
                _scheduledCouponListingStorage(),
                _onScheduledCouponListingTriggered,
                _max,
                _blockTimestamp()
            );
    }

    function _onScheduledCouponListingTriggered(
        uint256 /*_pos*/,
        uint256 /*_scheduledTasksLength*/,
        ScheduledTask memory _scheduledTask
    ) internal override {
        bytes memory data = _scheduledTask.data;

        bytes32 actionId = abi.decode(data, (bytes32));

        _addToCouponsOrderedList(uint256(actionId));
        uint256 pos = _getCouponsOrderedListTotal();

        _updateCorporateActionResult(actionId, COUPON_LISTING_RESULT_ID, abi.encodePacked(pos));
    }

    function _getScheduledCouponListingCount() internal view override returns (uint256) {
        return ScheduledTasksLib.getScheduledTaskCount(_scheduledCouponListingStorage());
    }

    function _getScheduledCouponListing(
        uint256 _pageIndex,
        uint256 _pageLength
    ) internal view override returns (ScheduledTask[] memory scheduledCouponListing_) {
        return ScheduledTasksLib.getScheduledTasks(_scheduledCouponListingStorage(), _pageIndex, _pageLength);
    }

    function _getPendingScheduledCouponListingTotalAt(
        uint256 _timestamp
    ) internal view override returns (uint256 total_) {
        total_ = 0;

        ScheduledTasksDataStorage storage scheduledCouponListing = _scheduledCouponListingStorage();

        uint256 scheduledTaskCount = ScheduledTasksLib.getScheduledTaskCount(scheduledCouponListing);

        for (uint256 i = 1; i <= scheduledTaskCount; i++) {
            uint256 pos = scheduledTaskCount - i;

            ScheduledTask memory scheduledTask = ScheduledTasksLib.getScheduledTasksByIndex(
                scheduledCouponListing,
                pos
            );

            if (scheduledTask.scheduledTimestamp < _timestamp) {
                total_++;
            } else {
                break;
            }
        }
    }

    function _getScheduledCouponListingIdAtIndex(uint256 _index) internal view override returns (uint256 couponID_) {
        ScheduledTask memory couponListing = ScheduledTasksLib.getScheduledTasksByIndex(
            _scheduledCouponListingStorage(),
            _index
        );

        bytes32 actionId = abi.decode(couponListing.data, (bytes32));

        (, couponID_, ) = _getCorporateAction(actionId);
    }

    function _scheduledCouponListingStorage()
        internal
        pure
        returns (ScheduledTasksDataStorage storage scheduledCouponListing_)
    {
        bytes32 position = _SCHEDULED_COUPON_LISTING_STORAGE_POSITION;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            scheduledCouponListing_.slot := position
        }
    }
}
