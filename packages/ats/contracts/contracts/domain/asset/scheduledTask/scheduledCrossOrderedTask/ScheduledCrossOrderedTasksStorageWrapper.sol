// SPDX-License-Identifier: Apache-2.0
pragma solidity >=0.8.0 <0.9.0;

import { ScheduledTasksLib } from "../../../../facets/layer_2/scheduledTask/ScheduledTasksLib.sol";
import { _SCHEDULED_CROSS_ORDERED_TASKS_STORAGE_POSITION } from "../../../../constants/storagePositions.sol";
import {
    ScheduledBalanceAdjustmentsStorageWrapper
} from "../scheduledBalanceAdjustment/ScheduledBalanceAdjustmentsStorageWrapper.sol";
import { SNAPSHOT_TASK_TYPE, BALANCE_ADJUSTMENT_TASK_TYPE } from "../../../../constants/values.sol";
import {
    ScheduledTask,
    ScheduledTasksDataStorage
} from "../../../../facets/layer_2/scheduledTask/scheduledTasksCommon/IScheduledTasksCommon.sol";
/* solhint-disable max-line-length */
import {
    IScheduledCrossOrderedTasks
} from "../../../../facets/layer_2/scheduledTask/scheduledCrossOrderedTask/IScheduledCrossOrderedTasks.sol";
/* solhint-enable max-line-length */

abstract contract ScheduledCrossOrderedTasksStorageWrapper is ScheduledBalanceAdjustmentsStorageWrapper {
    function _addScheduledCrossOrderedTask(uint256 _newScheduledTimestamp, bytes32 _taskType) internal override {
        ScheduledTasksLib.addScheduledTask(
            _scheduledCrossOrderedTaskStorage(),
            _newScheduledTimestamp,
            abi.encode(_taskType)
        );
    }

    function _triggerScheduledCrossOrderedTasks(uint256 _max) internal override returns (uint256) {
        return
            _triggerScheduledTasks(
                _scheduledCrossOrderedTaskStorage(),
                _onScheduledCrossOrderedTaskTriggered,
                _max,
                _blockTimestamp()
            );
    }

    function _callTriggerPendingScheduledCrossOrderedTasks() internal override returns (uint256) {
        return IScheduledCrossOrderedTasks(address(this)).triggerPendingScheduledCrossOrderedTasks();
    }

    function _onScheduledCrossOrderedTaskTriggered(
        uint256 /*_pos*/,
        uint256 /*_scheduledTasksLength*/,
        ScheduledTask memory _scheduledTask
    ) internal override {
        bytes memory data = _scheduledTask.data;

        bytes32 taskType = abi.decode(data, (bytes32));

        if (taskType == SNAPSHOT_TASK_TYPE) {
            _triggerScheduledSnapshots(1);
            return;
        }
        if (taskType == BALANCE_ADJUSTMENT_TASK_TYPE) {
            _triggerScheduledBalanceAdjustments(1);
            return;
        }

        _postOnScheduledCrossOrderedTaskTriggered(taskType);
    }

    // solhint-disable-next-line no-empty-blocks
    function _postOnScheduledCrossOrderedTaskTriggered(bytes32 taskType) internal virtual {}

    function _getScheduledCrossOrderedTaskCount() internal view override returns (uint256) {
        return ScheduledTasksLib.getScheduledTaskCount(_scheduledCrossOrderedTaskStorage());
    }

    function _getScheduledCrossOrderedTasks(
        uint256 _pageIndex,
        uint256 _pageLength
    ) internal view override returns (ScheduledTask[] memory scheduledTask_) {
        return ScheduledTasksLib.getScheduledTasks(_scheduledCrossOrderedTaskStorage(), _pageIndex, _pageLength);
    }

    function _scheduledCrossOrderedTaskStorage()
        internal
        pure
        returns (ScheduledTasksDataStorage storage scheduledCrossOrderedTasks_)
    {
        bytes32 position = _SCHEDULED_CROSS_ORDERED_TASKS_STORAGE_POSITION;
        // solhint-disable-next-line no-inline-assembly
        assembly {
            scheduledCrossOrderedTasks_.slot := position
        }
    }
}
